/* LR grammar */

File ::= #x02 Expression? #x03

Expression ::= ExpressionAdditive

ExpressionAdditive       ::= (ExpressionAdditive       ("+" | "-"))? ExpressionMultiplicative
ExpressionMultiplicative ::= (ExpressionMultiplicative ("*" | "/"))? ExpressionExponential
ExpressionExponential    ::=  ExpressionUnarySymbol    ("^"          ExpressionExponential)?

ExpressionUnarySymbol ::= ExpressionUnit | ("+" | "-") ExpressionUnarySymbol

ExpressionUnit ::= PrimitiveLiteral | StringTemplate | "(" Expression ")"

StringTemplate ::= [./lexical-grammar.ebnf#StringTemplateFull]
|                  [./lexical-grammar.ebnf#StringTemplateHead] Expression? ([./lexical-grammar.ebnf#StringTemplateMiddle] Expression?)* [./lexical-grammar.ebnf#StringTemplateTail]

PrimitiveLiteral ::= NUMBER
|                    [./lexical-grammar.ebnf#StringLiteral]

NUMBER ::= [./lexical-grammar.ebnf#Number]
