FileBound ::= #x02 /* START OF TEXT */
|             #x03 /* END OF TEXT   */

Whitespace ::= #x20 /* SPACE                     */
|              #x09 /* CHARACTER TABULATION `\t` */
|              #x0A /* LINE FEED (LF)       `\n` */
|              #x0D /* CARRIAGE RETURN (CR) `\r` */

/* more whitespace characters:
#x000B	LINE TABULATION
#x000C	FORM FEED (FF)
#x00A0	NO-BREAK SPACE
#x1680	OGHAM SPACE MARK
#x2000	EN QUAD
#x2001	EM QUAD
#x2002	EN SPACE
#x2003	EM SPACE
#x2004	THREE-PER-EM SPACE
#x2005	FOUR-PER-EM SPACE
#x2006	SIX-PER-EM SPACE
#x2007	FIGURE SPACE
#x2008	PUNCTUATION SPACE
#x2009	THIN SPACE
#x200A	HAIR SPACE
#x200B	ZERO WIDTH SPACE
#x2028	LINE SEPARATOR
#x2029	PARAGRAPH SEPARATOR
#x202F	NARROW NO-BREAK SPACE
#x205F	MEDIUM MATHEMATICAL SPACE
#x3000	IDEOGRAPHIC SPACE
#xFEFF	ZERO WIDTH NO-BREAK SPACE
*/



Comment ::= CommentSingle | CommentMulti | CommentDoc

CommentLine ::= '---' [^#x0A#x0D#x03]*

CommentMulti ::= '"' [^"#x03]* '"'

CommentMultiNest ::= '"{' CommentMultiNestChars? '}"'
CommentMultiNestChars ::= [^}"#x03] CommentMultiNestChars?
|                         "}" ([^"#x03] CommentMultiNestChars?)?
|                         '"' ([^{#x03] CommentMultiNestChars?)?
|                         CommentMultiNest CommentMultiNestChars?

CommentDoc ::= /*? following: #x0A [#x09#x20]* ?*/'"""' #x0A (/*? unequal: [#x09#x20]* '"""' ?*/[^#x03]* #x0A)? [#x09#x20]* '"""' /*? lookahead: #x0A ?*/



StringLiteral ::= "'" StringLiteralChars? "'"
StringLiteralChars ::= [^'\#x03] StringLiteralChars?
|                      "\" StringLiteralEscape StringLiteralChars?

StringLiteralEscape ::= EscapeChar | EscapeCode | LineContinuation | NonEscapeChar

EscapeChar       ::= "'" | "\" | "s" | "t" | "n" | "r"
EscapeCode       ::= "u{" [0-9a-f]* "}"
LineContinuation ::= #x0D? #x0A
NonEscapeChar    ::= [^'\stnru#x0D#x0A#x03] | "u" /*? lookahead: [^{] ?*/

StringTemplateFull   ::= "`"  StringTemplateCharacters? "`"
StringTemplateHead   ::= "`"  StringTemplateCharacters? "{{"
StringTempalteMiddle ::= "}}" StringTemplateCharacters? "{{"
StringTempalteTail   ::= "}}" StringTemplateCharacters? "`"

StringTemplateCharacters ::= [^`{\#x03] StringTemplateCharacters?
|                            "{" ([^`{#x03] TemplateCharacters?)?
|                            "\" ("`" | [^`#x03]) StringTemplateCharacters?



Number ::= IntegerLiteral

IntegerLiteral ::= "\b"  DigitSequenceBin
|                  "\q"  DigitSequenceQua
|                  "\o"  DigitSequenceOct
|                  "\d"? DigitSequenceDec
|                  "\x"  DigitSequenceHex
|                  "\z"  DigitSequenceHTD

DigitSequenceBin ::= DigitSequenceBin? [0-1]
DigitSequenceQua ::= DigitSequenceQua? [0-3]
DigitSequenceOct ::= DigitSequenceOct? [0-7]
DigitSequenceDec ::= DigitSequenceDec? [0-9]
DigitSequenceHex ::= DigitSequenceHex? [0-9a-f]
DigitSequenceHTD ::= DigitSequenceHTD? [0-9a-z]

Punctuator ::= "+" | "-" | "*" | "/" | "^" | "(" | ")"
