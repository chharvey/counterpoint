// KEYWORD         ::= [./lexicon.ebnf#Keyword];
// IDENTIFIER      ::= [./lexicon.ebnf#Identifier];
// INTEGER         ::= [./lexicon.ebnf#Integer];
// FLOAT           ::= [./lexicon.ebnf#Float];
// STRING          ::= [./lexicon.ebnf#String];
// TEMPLATE_FULL   ::= [./lexicon.ebnf#TemplateFull];
// TEMPLATE_HEAD   ::= [./lexicon.ebnf#TemplateHead];
// TEMPLATE_MIDDLE ::= [./lexicon.ebnf#TempalteMiddle];
// TEMPLATE_TAIL   ::= [./lexicon.ebnf#TempalteTail];



Word ::=
	| KEYWORD
	| IDENTIFIER
;



PrimitiveLiteral ::=
	| "null"
	| "false"
	| "true"
	| INTEGER
	| FLOAT
	| STRING
;



TypeKeyword ::=
	| "void"
	| "bool"
	| "int"
	| "float"
	| "str"
	| "obj"
;

EntryType<Named, Optional>
	::= <Named+>(Word . <Optional->":") <Optional+>"?:" Type;

ItemsType ::=
	|  EntryType<-Named><-Optional># ","?
	| (EntryType<-Named><-Optional># ",")? EntryType<-Named><+Optional># ","?
;

PropertiesType
	::= EntryType<+Named><-Optional, +Optional># ","?;

TypeTupleLiteral  ::= "[" (","? ItemsType)?    "]";
TypeRecordLiteral ::= "["  ","? PropertiesType "]";

TypeUnit ::=
	| IDENTIFIER
	| PrimitiveLiteral
	| TypeKeyword
	| TypeTupleLiteral
	| TypeRecordLiteral
	| "(" Type ")"
;

TypeUnarySymbol ::=
	| TypeUnit
	| TypeUnarySymbol "?"
	| TypeUnarySymbol "!"
;

TypeIntersection ::= (TypeIntersection "&")? TypeUnarySymbol;
TypeUnion        ::= (TypeUnion        "|")? TypeIntersection;

Type ::=
	| TypeUnion
;



StringTemplate ::=
	| TEMPLATE_FULL
	| TEMPLATE_HEAD Expression? (TEMPLATE_MIDDLE Expression?)* TEMPLATE_TAIL
;

Property ::= Word       "="   Expression;
Case     ::= Expression "|->" Expression;

TupleLiteral   ::= "[" ( ","? Expression# ","? )? "]";
RecordLiteral  ::= "["   ","? Property#   ","?    "]";
MappingLiteral ::= "["   ","? Case#       ","?    "]";

ExpressionUnit ::=
	| IDENTIFIER
	| PrimitiveLiteral
	| StringTemplate
	| TupleLiteral
	| RecordLiteral
	| MappingLiteral
	| "(" Expression ")"
;

PropertyAccess
	::= ("." | "?." | "!.") (INTEGER | Word | "[" Expression "]");

ExpressionCompound ::=
	| ExpressionUnit
	| ExpressionCompound PropertyAccess
;

ExpressionUnarySymbol ::=
	| ExpressionCompound
	| ("!" | "?" | "+" | "-") ExpressionUnarySymbol
;

ExpressionExponential
	::= ExpressionUnarySymbol ("^" ExpressionExponential)?;

ExpressionMultiplicative ::= (ExpressionMultiplicative ("*" | "/" ))?                                            ExpressionExponential;
ExpressionAdditive       ::= (ExpressionAdditive       ("+" | "-" ))?                                            ExpressionMultiplicative;
ExpressionComparative    ::= (ExpressionComparative    ("<" | ">" | "<=" | ">=" | "!<" | "!>" | "is" | "isnt"))? ExpressionAdditive;
ExpressionEquality       ::= (ExpressionEquality       ("===" | "!==" | "==" | "!="))?                           ExpressionComparative;
ExpressionConjunctive    ::= (ExpressionConjunctive    ("&&" | "!&"))?                                           ExpressionEquality;
ExpressionDisjunctive    ::= (ExpressionDisjunctive    ("||" | "!|"))?                                           ExpressionConjunctive;

ExpressionConditional
	::= "if" Expression "then" Expression "else" Expression;

Expression ::=
	| ExpressionDisjunctive
	| ExpressionConditional
;



DeclarationType     ::= "type"            IDENTIFIER "=" Type                ";";
DeclarationVariable ::= "let"  "unfixed"? IDENTIFIER ":" Type "=" Expression ";";

Declaration ::=
	| DeclarationType
	| DeclarationVariable
;

Assignee ::=
	| IDENTIFIER
;

StatementAssignment
	::= Assignee "=" Expression ";";

Statement ::=
	| Expression? ";"
	| Declaration
	| StatementAssignment
;



Goal ::= #x02 Statement* #x03;
