SemanticType =:=
	| SemanticTypeConstant
	| SemanticTypeOperation
;

SemanticExpression =:=
	| SemanticConstant
	| SemanticIdentifier
	| SemanticTemplate
	| SemanticOperation
;



SemanticTypeConstant[value: Type]
	::= ();

SemanticTypeOperation[operator: ORNULL]
	::= SemanticType;
SemanticTypeOperation[operator: AND | OR]
	::= SemanticType SemanticType;



SemanticConstant[value: Null | Boolean | Number | Sequence<RealNumber>]
	::= ();

SemanticIdentifier[id: RealNumber]
	::= ();

SemanticTemplate[type: "full"]
	::= SemanticConstant;
SemanticTemplate[type: "substitution"]
	::= (SemanticConstant SemanticExpression?)+ SemanticConstant;

SemanticTemplatePartial
	::= (SemanticConstant SemanticExpression?)+;



SemanticOperation[operator: NOT | EMP]
	::= SemanticExpression;
SemanticOperation[operator: NEG]
	::= SemanticExpression[type: Number];
SemanticOperation[operator: EXP | MUL | DIV | ADD | LT | LE | GT | GE]
	::= SemanticExpression[type: Number] SemanticExpression[type: Number];
SemanticOperation[operator: IS | EQ]
	::= SemanticExpression[type: Object] SemanticExpression[type: Object];
SemanticOperation[operator: AND | OR]
	::= SemanticExpression SemanticExpression;
SemanticOperation[operator: COND]
	::= SemanticExpression[type: Boolean] SemanticExpression SemanticExpression;



SemanticStatement =:=
	| SemanticStatementExpression
	| SemanticDeclaration
	| SemanticAssignment
;



SemanticStatementExpression
	::= SemanticExpression?;



SemanticDeclarationVariable[unfixed: Boolean]
	::= SemanticAssignee SemanticType SemanticExpression;

SemanticDeclaration =:=
	| SemanticDeclarationVariable
;



SemanticAssignment
	::= SemanticAssignee SemanticExpression;

SemanticAssignee
	::= SemanticIdentifier;



SemanticGoal
	::= SemanticStatement*;
