SemanticType =:=
	| SemanticTypeConstant
	| SemanticTypeAlias
	| SemanticTypeEmptyCollection
	| SemanticTypeList
	| SemanticTypeRecord
	| SemanticTypeOperation
;

SemanticExpression =:=
	| SemanticConstant
	| SemanticVariable
	| SemanticTemplate
	| SemanticEmptyCollection
	| SemanticList
	| SemanticRecord
	| SemanticMapping
	| SemanticOperation
;



SemanticKey[id: RealNumber]
	::= ();



SemanticTypeProperty
	::= SemanticKey SemanticType;



SemanticTypeConstant[value: Type]
	::= ();

SemanticTypeAlias[id: RealNumber]
	::= ();

SemanticTypeEmptyCollection ::= ();
SemanticTypeList            ::= SemanticType+;
SemanticTypeRecord          ::= SemanticTypeProperty+;

SemanticTypeOperation[operator: ORNULL]
	::= SemanticType;
SemanticTypeOperation[operator: AND | OR]
	::= SemanticType SemanticType;



SemanticProperty
	::= SemanticKey SemanticExpression;

SemanticCase
	::= SemanticExpression+ SemanticExpression;



SemanticConstant[value: Null | Boolean | Number | Sequence<RealNumber>]
	::= ();

SemanticVariable[id: RealNumber]
	::= ();

SemanticTemplate[type: "full"]
	::= SemanticConstant;
SemanticTemplate[type: "substitution"]
	::= (SemanticConstant SemanticExpression?)+ SemanticConstant;

SemanticTemplatePartial
	::= (SemanticConstant SemanticExpression?)+;

SemanticEmptyCollection ::= ();
SemanticList            ::= SemanticExpression+;
SemanticRecord          ::= SemanticProperty+;
SemanticMapping         ::= SemanticCase+;

SemanticOperation[operator: NOT | EMP]
	::= SemanticExpression;
SemanticOperation[operator: NEG]
	::= SemanticExpression[type: Number];
SemanticOperation[operator: EXP | MUL | DIV | ADD | LT | LE | GT | GE]
	::= SemanticExpression[type: Number] SemanticExpression[type: Number];
SemanticOperation[operator: IS | EQ]
	::= SemanticExpression[type: Object] SemanticExpression[type: Object];
SemanticOperation[operator: AND | OR]
	::= SemanticExpression SemanticExpression;
SemanticOperation[operator: COND]
	::= SemanticExpression[type: Boolean] SemanticExpression SemanticExpression;



SemanticStatement =:=
	| SemanticStatementExpression
	| SemanticDeclaration
	| SemanticAssignment
;



SemanticStatementExpression
	::= SemanticExpression?;



SemanticDeclarationVariable[unfixed: Boolean]
	::= SemanticVariable SemanticType SemanticExpression;

SemanticDeclarationType
	::= SemanticTypeAlias SemanticType;

SemanticDeclaration =:=
	| SemanticDeclarationVariable
	| SemanticDeclarationType
;



SemanticAssignment
	::= SemanticAssignee SemanticExpression;

SemanticAssignee
	::= SemanticVariable;



SemanticGoal
	::= SemanticStatement*;
