IDENTIFIER  ::= [./ebnf-lexicon.ebnf#Identifier];
CHAR_CODE   ::= [./ebnf-lexicon.ebnf#CharCode];
STRING      ::= [./ebnf-lexicon.ebnf#String];
CHAR_CLASS  ::= [./ebnf-lexicon.ebnf#CharClass];



NonterminalName ::= IDENTIFIER ("<" IDENTIFIER#                     ">")?;
NonterminalRef  ::= IDENTIFIER ("<" (("+" | "-" | "?") IDENTIFIER)# ">")?;
ConditionSet    ::=             "<" (IDENTIFIER ("+" | "-")      )# ">"  ;



Unit ::=
	| CHAR_CODE
	| STRING
	| CHAR_CLASS
	| NonterminalRef
	| "(" Definition ")"
;



Unary
	::= Unit ("+" | "*" | "#")? "?"?;



Item ::=
	| Unary
	| ConditionSet Item
;


Order  ::= ( Order  "."? )? Item;
Concat ::= ( Concat "&"  )? Order;
Altern ::= ( Altern "|"  )? Concat;



Definition
	::= Altern;



Production
	::= NonterminalName (":::=" | "::=") "|"? Definition ";";



Grammar
	::= #x02 Production* #x03;
