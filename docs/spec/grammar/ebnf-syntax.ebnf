IDENTIFIER  ::= [./ebnf-lexicon.ebnf#Identifier];
CHAR_CODE   ::= [./ebnf-lexicon.ebnf#CharCode];
STRING      ::= [./ebnf-lexicon.ebnf#String];
CHAR_CLASS  ::= [./ebnf-lexicon.ebnf#CharClass];



NonterminalDefinition ::= IDENTIFIER ("<" IDENTIFIER#                     ">")?;
NonterminalReference  ::= IDENTIFIER ("<" (("+" | "-" | "?") IDENTIFIER)# ">")?;
Condition             ::=             "<" (IDENTIFIER ("+" | "-")      )# ">"  ;



Unit ::=
	| CHAR_CODE
	| STRING
	| CHAR_CLASS
	| NonterminalReference
	| "(" Altern ")"
;



Unary
	::= Unit ("+" | "*" | "#")? "?"?;



Item ::=
	| Unary
	| Condition Item
;



Concat ::= (Concat "&")? Item+;
Altern ::= (Altern "|")? Concat;



Production
	::= NonterminalDefinition (":::=" | "::=") "|"? Altern ";";



Grammar
	::= #x02 Production* #x03;
