Void VarCheck(Or<SemanticType, SemanticConstant> node) :=
	1. *Return.*
;



Void! VarCheck(SemanticVariable var) :=
	1. *If* the validator does not contain a record for `var`:
		1. *Throw:* a new ReferenceError01.
	// TODO: Throw a ReferenceError02 if the variable is declared further down in source.
;



Void! VarCheck(Or<SemanticTemplate, SemanticOperation, SemanticStatementExpression, SemanticAssignment> list) :=
	1. *For* `i` in `list`:
		1. Perform *Unwrap:* `VarCheck(list[i])`.
;



Void! VarCheck(SemanticDeclarationVariable decl) :=
	1. *Assert:* `decl.children.count` is 3.
	2. *Let* `assignee` be `decl.children.0`.
	3. *Assert:* `assignee.children.count` is 1.
	4. *Let* `id` be `assignee.children.0.id`.
	5. *If* the validator contains a record for `id`:
		1. *Let* `info` be the record for `id` in the validator.
		2. *If* `info.kind` is *value*:
			1. *Throw:* a new AssignmentError01.
		3. *Assert:* `info.kind` is *type*.
		4. *Throw:* a new AssignmentError02.
	6. *Let* `symbol` be a new SymbolStructure [
		id=      `id`,
		kind=    *value*,
		type=    *UnwrapAffirm:* `Assess(decl.children.1)`,
		unfixed= `decl.unfixed`,
	].
	7. Add `symbol` as a record for `id` to the validator.
	8. *Return:* `VarCheck(decl.children.2)`.
;



Void! VarCheck(SemanticDeclarationType decl) :=
	1. *Assert:* `decl.children.count` is 2.
	2. *Let* `assignee` be `decl.children.0`.
	3. *Assert:* `assignee.children.count` is 1.
	4. *Let* `id` be `assignee.children.0.id`.
	5. *If* the validator contains a type record for `id`:
		1. *Let* `info` be the record for `id` in the validator.
		2. *If* `info.kind` is *type*:
			1. *Throw:* a new AssignmentError02.
		3. *Assert:* `info.kind` is *value*.
		4. *Throw:* a new AssignmentError01.
	6. *Let* `symbol` be a new SymbolStructure [
		id=      `id`,
		kind=    *type*,
		type=    *UnwrapAffirm:* `Assess(decl.children.1)`,
		unfixed= `false`,
	].
	7. Add `symbol` as a record for `id` to the validator.
	8. *Return*.
;



Void! VarCheck(SemanticAssignee assignee) :=
	1. *Assert:* `assignee.children.count` is 1.
	2. *Let* `id` be `assignee.children.0`.
	3. Perform *Unwrap:* `VarCheck(id)`.
	4. *Assert:* The validator contains a record for `id`.
	5. *Let* `info` be the record for `id` in the validator.
	6. *If* `info.unfixed` is `false`:
		1. *Throw:* a new AssignmentError10.
;



Void! VarCheck(SemanticGoal list) :=
	1. *For* `i` in `list`:
		1. Perform *Unwrap:* `VarCheck(list[i])`.
	2. Remove all symbol records in the validator.
;
