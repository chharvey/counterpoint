Filebound
	:::= #x02 | #x03;



Whitespace
	:::= (#x20 | #x09 | #x0a)+;



Comment :::=
	| CommentLine
	| CommentMulti
;

CommentLine
	:::= "%" ([^%#x0a#x03] [^#x0a#x03]*)? #x0a;

CommentMulti
	:::= "%%" ("%"? [^%#x03])* "%%";



Punctuator :::=
	// grouping
		| "(" | ")"
		| "[" | "]"
		| "," | "|->"
	// unary
		| "!" | "?" | "+" | "-"
	// binary
		| "^" | "*" | "/"
		| "<" | ">" | "<=" | ">=" | "!<" | "!>"
		| "===" | "!==" | "==" | "!="
		| "&&" | "!&" | "||" | "!|"
		| "&" | "|"
	// statement
		| ";" | ":" | "="
;



Keyword :::=
	// literal
		| "null"
		| "bool"
		| "false"
		| "true"
		| "int"
		| "float"
		| "str"
		| "obj"
	// operator
		| "is"
		| "isnt"
		| "if"
		| "then"
		| "else"
	// storage
		| "let"
		| "type"
	// modifier
		| "unfixed"
;



Identifier
	:::= ([A-Za-z_] [A-Za-z0-9_]* | "`" [^`#x03]* "`") - Keyword;



Number<Radix, Separator> :::=
	| Integer<?Radix, ?Separator>
	| Float<?Separator>
;

Integer<Radix, Separator>
	:::= ("+" | "-")? IntegerDigits<?Radix, ?Separator>;

IntegerDigits<Radix, Separator> :::=
	| <Radix->DigitSequenceDec<?Separator>
	| <Radix+>("\b"  DigitSequenceBin<?Separator>)
	| <Radix+>("\q"  DigitSequenceQua<?Separator>)
	| <Radix+>("\o"  DigitSequenceOct<?Separator>)
	| <Radix+>("\d"? DigitSequenceDec<?Separator>)
	| <Radix+>("\x"  DigitSequenceHex<?Separator>)
	| <Radix+>("\z"  DigitSequenceHTD<?Separator>)
;

Float<Separator>
	:::= SignedDigitSequenceDec<?Separator> "." ( FractionalPart<?Separator> ExponentPart<?Separator>? )?;

FractionalPart<Separator>
	:::= DigitSequenceDec<?Separator>;

ExponentPart<Separator>
	:::= "e" SignedDigitSequenceDec<?Separator>;

SignedDigitSequenceDec<Separator>
	:::= ("+" | "-")? DigitSequenceDec<?Separator>;

DigitSequenceBin<Separator> :::= ( DigitSequenceBin <Separator+>"_"? )? [0-1];
DigitSequenceQua<Separator> :::= ( DigitSequenceQua <Separator+>"_"? )? [0-3];
DigitSequenceOct<Separator> :::= ( DigitSequenceOct <Separator+>"_"? )? [0-7];
DigitSequenceDec<Separator> :::= ( DigitSequenceDec <Separator+>"_"? )? [0-9];
DigitSequenceHex<Separator> :::= ( DigitSequenceHex <Separator+>"_"? )? [0-9a-f];
DigitSequenceHTD<Separator> :::= ( DigitSequenceHTD <Separator+>"_"? )? [0-9a-z];



String<Comment>
	:::= "'" StringChars<?Comment>? StringUnfinished<?Comment>? "'";

	StringChars<Comment>
		:::= StringChar<?Comment> StringChars<?Comment>?;

		StringChar<Comment> :::=
			| <Comment->[^'\#x03]
			| <Comment+>[^'\%#x03]
			| "\"  StringEscape<?Comment>
			| "\u" [^'{#x03]
			| <Comment+>("%"  ([^'%#x0a#x03] [^'#x0a#x03]*)? #x0a)
			| <Comment+>("%%" ("%"? [^'%#x03])*              "%%")
		;

			StringEscape<Comment> :::=
				| "'" | "\" | <Comment+>"%"
				| "s" | "t" | "n" | "r"
				| "u{" DigitSequenceHex? "}"
				| #x0a
				| <Comment->[^'\stnru#x0a#x03]
				| <Comment+>[^'\%stnru#x0a#x03]
			;

	StringUnfinished<Comment> :::=
		| "\u"
		| <Comment+>("%"  ([^'%#x0a#x03] [^'#x0a#x03]*)?)
		| <Comment+>("%%" ("%"? [^'%#x03])*)
	;



TemplateFull   :::= "'''" TemplateChars__EndDelim?  "'''";
TemplateHead   :::= "'''" TemplateChars__EndInterp? "{{" ;
TemplateMiddle :::= "}}"  TemplateChars__EndInterp? "{{" ;
TemplateTail   :::= "}}"  TemplateChars__EndDelim?  "'''";

	TemplateChars__EndDelim :::=
		| [^'{#x03] TemplateChars__EndDelim?
		| TemplateChars__EndDelim__StartDelim
		| TemplateChars__EndDelim__StartInterp
	;

		TemplateChars__EndDelim__StartDelim :::=
			| ("'"  | "''")   [^'{#x03] TemplateChars__EndDelim?
			| ("'{" | "''{") ([^'{#x03] TemplateChars__EndDelim? | TemplateChars__EndDelim__StartDelim)?
		;

		TemplateChars__EndDelim__StartInterp :::=
			| "{"            ([^'{#x03] TemplateChars__EndDelim?                                       )?
			| ("{'" | "{''") ([^'{#x03] TemplateChars__EndDelim? | TemplateChars__EndDelim__StartInterp)
		;

	TemplateChars__EndInterp :::=
		| [^'{#x03] TemplateChars__EndInterp?
		| TemplateChars__EndInterp__StartDelim
		| TemplateChars__EndInterp__StartInterp
	;

		TemplateChars__EndInterp__StartDelim :::=
			| ("'"  | "''")  ([^'{#x03] TemplateChars__EndInterp?                                       )?
			| ("'{" | "''{") ([^'{#x03] TemplateChars__EndInterp? | TemplateChars__EndInterp__StartDelim)
		;

		TemplateChars__EndInterp__StartInterp :::=
			| "{"             [^'{#x03] TemplateChars__EndInterp?
			| ("{'" | "{''") ([^'{#x03] TemplateChars__EndInterp? | TemplateChars__EndInterp__StartInterp)?
		;



Goal<Comment, Radix, Separator> :::=
	| Filebound
	| Whitespace
	| <Comment+>Comment
	| Punctuator
	| Keyword
	| Identifier
	| Number<?Radix, ?Separator>
	| String<?Comment>
	| TemplateFull
	| TemplateHead
	| TemplateMiddle
	| TemplateTail
;
