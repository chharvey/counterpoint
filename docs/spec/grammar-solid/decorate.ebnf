_KEYWORD_OTHER =:=
	// operator
		| "mutable"
		| "is"
		| "isnt"
		| "if"
		| "then"
		| "else"
	// storage
		| "type"
		| "let"
	// modifier
		| "unfixed"
;

Decorate(Word ::= _KEYWORD_OTHER) -> SemanticKey
	:= (SemanticKey[id=TokenWorth(_KEYWORD_OTHER)]);
Decorate(Word ::= KEYWORD_TYPE) -> SemanticKey
	:= (SemanticKey[id=TokenWorth(KEYWORD_TYPE)]);
Decorate(Word ::= KEYWORD_VALUE) -> SemanticKey
	:= (SemanticKey[id=TokenWorth(KEYWORD_VALUE)]);
Decorate(Word ::= IDENTIFIER) -> SemanticKey
	:= (SemanticKey[id=TokenWorth(IDENTIFIER)]);



Decorate(PrimitiveLiteral ::= KEYWORD_VALUE) -> SemanticConstant
	:= (SemanticConstant[value=KeywordValue(KEYWORD_VALUE)]);
Decorate(PrimitiveLiteral ::= INTEGER) -> SemanticConstant
	:= (SemanticConstant[value=Integer(TokenWorth(INTEGER))]);
Decorate(PrimitiveLiteral ::= FLOAT) -> SemanticConstant
	:= (SemanticConstant[value=Float(TokenWorth(FLOAT))]);
Decorate(PrimitiveLiteral ::= STRING) -> SemanticConstant
	:= (SemanticConstant[value=String(TokenWorth(STRING))]);



Decorate(EntryType ::= Type) -> SemanticItemType
	:= (SemanticItemType[optional=false] Decorate(Type));
Decorate(EntryType_Optional ::= "?:" Type) -> SemanticItemType
	:= (SemanticItemType[optional=true] Decorate(Type));
Decorate(EntryType_Named ::= Word ":" Type) -> SemanticPropertyType
	:= (SemanticPropertyType[optional=false]
		Decorate(Word)
		Decorate(Type)
	);
Decorate(EntryType_Named_Optional ::= Word "?:" Type) -> SemanticPropertyType
	:= (SemanticPropertyType[optional=true]
		Decorate(Word)
		Decorate(Type)
	);



Decorate(ItemsType ::= EntryType# ","?) -> Sequence<SemanticItemType>
	:= ParseList(EntryType, SemanticItemType);
Decorate(ItemsType ::= EntryType_Optional# ","?) -> Sequence<SemanticItemType>
	:= ParseList(EntryType_Optional, SemanticItemType);
Decorate(ItemsType ::= EntryType# "," EntryType_Optional# ","?) -> Sequence<SemanticItemType>
	:= [
		...ParseList(EntryType,          SemanticItemType),
		...ParseList(EntryType_Optional, SemanticItemType),
	];

Decorate(PropertiesType ::= EntryType_Named# ","?) -> Sequence<SemanticPropertyType>
	:= ParseList(EntryType_Named, SemanticPropertyType);
Decorate(PropertiesType ::= EntryType_Named_Optional# ","?) -> Sequence<SemanticPropertyType>
	:= ParseList(EntryType_Named_Optional, SemanticPropertyType);



Decorate(TypeTupleLiteral ::= "[" "]") -> SemanticTypeTuple
	:= (SemanticTypeTuple);
Decorate(TypeTupleLiteral ::= "[" ","? ItemsType "]") -> SemanticTypeTuple
	:= (SemanticTypeTuple
		...Decorate(ItemsType)
	);

Decorate(TypeRecordLiteral ::= "[" ","? PropertiesType "]") -> SemanticTypeRecord
	:= (SemanticTypeRecord
		...Decorate(PropertiesType)
	);

Decorate(TypeHashLiteral ::= "[" ":" Type "]") -> SemanticTypeHash
	:= (SemanticTypeHash
		Decorate(Type)
	);

Decorate(TypeMapLiteral ::= "{" Type__0 "->" Type__1 "}") -> SemanticTypeMap
	:= (SemanticTypeMap
		Decorate(Type__0)
		Decorate(Type__1)
	);

Decorate(GenericArguments ::= "<" ","? Type# ","? ">") -> Sequence<SemanticType>
	:= ParseList(Type, SemanticType);



Decorate(TypeUnit ::= KEYWORD_TYPE) -> SemanticTypeConstant
	:= (SemanticTypeConstant[value=KeywordType(KEYWORD_TYPE)]);
Decorate(TypeUnit ::= IDENTIFIER) -> SemanticTypeAlias
	:= (SemanticTypeAlias[id=TokenWorth(IDENTIFIER)]);
Decorate(TypeUnit ::= PrimitiveLiteral) -> SemanticTypeConstant
	:= (SemanticTypeConstant[value=ToType(Decorate(PrimitiveLiteral).value)]);
Decorate(TypeUnit ::= TypeTupleLiteral) -> SemanticTypeTuple
	:= Decorate(TypeTupleLiteral);
Decorate(TypeUnit ::= TypeRecordLiteral) -> SemanticTypeRecord
	:= Decorate(TypeRecordLiteral);
Decorate(TypeUnit ::= "(" Type ")") -> SemanticType
	:= Decorate(Type);



Decorate(PropertyAccessType ::= "." INTEGER) -> SemanticIndexType
	:= (SemanticIndexType
		(SemanticTypeConstant[value=ToType(Integer(TokenWorth(INTEGER)))])
	);
Decorate(PropertyAccessType ::= "." Word) -> SemanticKey
	:= Decorate(Word);

Decorate(GenericCall ::= "." GenericArguments) -> Sequence<SemanticType>
	:= Decorate(GenericArguments);



Decorate(TypeCompound ::= TypeUnit) -> SemanticType
	:= Decorate(TypeUnit);
Decorate(TypeCompound ::= TypeCompound PropertyAccessType) -> SemanticTypeAccess
	:= (SemanticTypeAccess
		Decorate(TypeCompound)
		Decorate(PropertyAccessType)
	);
Decorate(TypeCompound ::= TypeCompound GenericCall) -> SemanticTypeCall
	:= (SemanticTypeCall
		Decorate(TypeCompound)
		...Decorate(GenericCall)
	);



Decorate(TypeUnarySymbol ::= TypeCompound) -> SemanticType
	:= Decorate(TypeCompound);
Decorate(TypeUnarySymbol ::= TypeUnarySymbol "?") -> SemanticTypeOperation
	:= (SemanticTypeOperation[operator=ORNULL]
		Decorate(TypeUnarySymbol)
	);
Decorate(TypeUnarySymbol ::= TypeUnarySymbol "!") -> SemanticTypeOperation
	:= (SemanticTypeOperation[operator=OREXCP]
		Decorate(TypeUnarySymbol)
	);
Decorate(TypeUnarySymbol ::= TypeUnarySymbol "[" "]") -> SemanticTypeList
	:= (SemanticTypeList[count=none]
		Decorate(TypeUnarySymbol)
	);
Decorate(TypeUnarySymbol ::= TypeUnarySymbol "[" INTEGER "]") -> SemanticTypeList
	:= (SemanticTypeList[count=TokenWorth(INTEGER)]
		Decorate(TypeUnarySymbol)
	);
Decorate(TypeUnarySymbol ::= TypeUnarySymbol "{" "}") -> SemanticTypeSet
	:= (SemanticTypeSet
		Decorate(TypeUnarySymbol)
	);



Decorate(TypeUnaryKeyword ::= TypeUnarySymbol) -> SemanticType
	:= Decorate(TypeUnarySymbol);
Decorate(TypeUnaryKeyword ::= "mutable" TypeUnaryKeyword) -> SemanticTypeOperation
	:= (SemanticTypeOperation[operator=MUTABLE]
		Decorate(TypeUnaryKeyword)
	);



Decorate(TypeIntersection ::= TypeUnarySymbol) -> SemanticType
	:= Decorate(TypeUnarySymbol);
Decorate(TypeIntersection ::= TypeIntersection "&" TypeUnaryKeyword) -> SemanticTypeOperation
	:= (SemanticTypeOperation[operator=AND]
		Decorate(TypeIntersection)
		Decorate(TypeUnaryKeyword)
	);



Decorate(TypeUnion ::= TypeIntersection) -> SemanticType
	:= Decorate(TypeIntersection);
Decorate(TypeUnion ::= TypeUnion "|" TypeIntersection) -> SemanticTypeOperation
	:= (SemanticTypeOperation[operator=OR]
		Decorate(TypeUnion)
		Decorate(TypeIntersection)
	);



Decorate(Type ::= TypeUnion) -> SemanticType
	:= Decorate(TypeUnion);



Decorate(StringTemplate ::= TEMPLATE_FULL) -> SemanticTemplate
	:= (SemanticTemplate
		(SemanticConstant[value=String(TokenWorth(TEMPLATE_FULL))])
	);
Decorate(StringTemplate ::= TEMPLATE_HEAD TEMPLATE_TAIL) -> SemanticTemplate
	:= (SemanticTemplate
		(SemanticConstant[value=String(TokenWorth(TEMPLATE_HEAD))])
		(SemanticConstant[value=String(TokenWorth(TEMPLATE_TAIL))])
	);
Decorate(StringTemplate ::= TEMPLATE_HEAD Expression TEMPLATE_TAIL) -> SemanticTemplate
	:= (SemanticTemplate
		(SemanticConstant[value=String(TokenWorth(TEMPLATE_HEAD))])
		Decorate(Expression)
		(SemanticConstant[value=String(TokenWorth(TEMPLATE_TAIL))])
	);
Decorate(StringTemplate ::= TEMPLATE_HEAD StringTemplate__0__List TEMPLATE_TAIL) -> SemanticTemplate
	:= (SemanticTemplate
		(SemanticConstant[value=String(TokenWorth(TEMPLATE_HEAD))])
		...Decorate(StringTemplate__0__List)
		(SemanticConstant[value=String(TokenWorth(TEMPLATE_TAIL))])
	);
Decorate(StringTemplate ::= TEMPLATE_HEAD Expression StringTemplate__0__List TEMPLATE_TAIL) -> SemanticTemplate
	:= (SemanticTemplate
		(SemanticConstant[value=String(TokenWorth(TEMPLATE_HEAD))])
		Decorate(Expression)
		...Decorate(StringTemplate__0__List)
		(SemanticConstant[value=String(TokenWorth(TEMPLATE_TAIL))])
	);

	Decorate(StringTemplate__0__List ::= TEMPLATE_MIDDLE) -> Vector<SemanticConstant>
		:= [SemanticConstant[value=String(TokenWorth(TEMPLATE_MIDDLE))]];
	Decorate(StringTemplate__0__List ::= TEMPLATE_MIDDLE Expression) -> Vector<SemanticConstant, SemanticExpression>
		:= [
			(SemanticConstant[value=String(TokenWorth(TEMPLATE_MIDDLE))]),
			Decorate(Expression),
		];
	Decorate(StringTemplate__0__List ::= StringTemplate__0__List TEMPLATE_MIDDLE) -> Sequence<...Vector<SemanticConstant, SemanticExpression?>>
		:= [
			...Decorate(StringTemplate__0__List),
			(SemanticConstant[value=String(TokenWorth(TEMPLATE_MIDDLE))]),
		];
	Decorate(StringTemplate__0__List ::= StringTemplate__0__List TEMPLATE_MIDDLE Expression) -> Sequence<...Vector<SemanticConstant, SemanticExpression?>>
		:= [
			...Decorate(StringTemplate__0__List),
			(SemanticConstant[value=String(TokenWorth(TEMPLATE_MIDDLE))]),
			Decorate(Expression),
		];



Decorate(Property ::= Word "=" Expression) -> SemanticProperty
	:= (SemanticProperty
		Decorate(Word)
		Decorate(Expression)
	);

Decorate(Case ::= Expression "->" Expression) -> SemanticCase
	:= (SemanticCase
		Decorate(Expression)
		Decorate(Expression)
	);

Decorate(TupleLiteral ::= "[" "]") -> SemanticTuple
	:= (SemanticTuple);
Decorate(TupleLiteral ::= "[" ","? Expression# ","? "]") -> SemanticTuple
	:= (SemanticTuple
		...ParseList(Expression, SemanticExpression)
	);

Decorate(RecordLiteral ::= "[" ","? Property# ","? "]") -> SemanticRecord
	:= (SemanticRecord
		...ParseList(Property, SemanticProperty)
	);

Decorate(SetLiteral ::= "{" ","? Expression# ","? "}") -> SemanticSet
	:= (SemanticSet
		...ParseList(Expression, SemanticExpression)
	);

Decorate(MapLiteral ::= "{" ","? Case# ","? "}") -> SemanticMap
	:= (SemanticMap
		...ParseList(Case, SemanticCase)
	);

Decorate(FunctionArguments ::= "(" ( ","? Expression# ","? )? ")") -> Sequence<SemanticExpression>
	:= ParseList(Expression, SemanticExpression);



Decorate(ExpressionUnit ::= IDENTIFIER) -> SemanticVariable
	:= (SemanticVariable[id=TokenWorth(IDENTIFIER)]);
Decorate(ExpressionUnit ::= PrimitiveLiteral) -> SemanticConstant
	:= Decorate(PrimitiveLiteral);
Decorate(ExpressionUnit ::= StringTemplate) -> SemanticTemplate
	:= Decorate(StringTemplate);
Decorate(ExpressionUnit ::= TupleLiteral) -> SemanticTuple
	:= Decorate(TupleLiteral);
Decorate(ExpressionUnit ::= RecordLiteral) -> SemanticRecord
	:= Decorate(RecordLiteral);
Decorate(ExpressionUnit ::= SetLiteral) -> SemanticSet
	:= Decorate(SetLiteral);
Decorate(ExpressionUnit ::= MapLiteral) -> SemanticMap
	:= Decorate(MapLiteral);
Decorate(ExpressionUnit ::= "(" Expression ")") -> SemanticExpression
	:= Decorate(Expression);



Decorate(PropertyAccess ::= ("." | "?." | "!.") INTEGER) -> SemanticIndex
	:= (SemanticIndex
		(SemanticConstant[value=Integer(TokenWorth(INTEGER))])
	);
Decorate(PropertyAccess ::= ("." | "?." | "!.") Word) -> SemanticKey
	:= Decorate(Word);
Decorate(PropertyAccess ::= ("." | "?." | "!.") "[" Expression "]") -> SemanticExpression
	:= Decorate(Expression);

Decorate(PropertyAssign ::= "." INTEGER) -> SemanticIndex
	:= (SemanticIndex
		(SemanticConstant[value=Integer(TokenWorth(INTEGER))])
	);
Decorate(PropertyAssign ::= "." Word) -> SemanticKey
	:= Decorate(Word);
Decorate(PropertyAssign ::= "." "[" Expression "]") -> SemanticExpression
	:= Decorate(Expression);

Decorate(FunctionCall ::= "." FunctionArguments) -> Vector<Sequence<SemanticType>, Sequence<SemanticExpression>>
	:= [
		[],
		Decorate(FunctionArguments),
	];
Decorate(FunctionCall ::= "." GenericArguments FunctionArguments) -> Vector<Sequence<SemanticType>, Sequence<SemanticExpression>>
	:= [
		Decorate(GenericArguments),
		Decorate(FunctionArguments),
	];



Decorate(ExpressionCompound ::= ExpressionUnit) -> SemanticExpression
	:= Decorate(ExpressionUnit);
Decorate(ExpressionCompound ::= ExpressionCompound PropertyAccess) -> SemanticAccess
	:= (SemanticAccess[kind=AccessKind(PropertyAccess)]
		Decorate(ExpressionCompound)
		Decorate(PropertyAccess)
	);
Decorate(ExpressionCompound ::= ExpressionCompound FunctionCall) -> SemanticCall
	:= (SemanticCall
		Decorate(ExpressionCompound)
		...(...Decorate(FunctionCall))
	);



Decorate(Assignee ::= IDENTIFIER) -> SemanticVariable
	:= (SemanticVariable[id=TokenWorth(IDENTIFIER)]);
Decorate(Assignee ::= ExpressionCompound PropertyAssign) -> SemanticAccess
	:= (SemanticAccess[kind=NORMAL]
		Decorate(ExpressionCompound)
		Decorate(PropertyAssign)
	);



Decorate(ExpressionUnarySymbol ::= ExpressionCompound) -> SemanticExpression
	:= Decorate(ExpressionCompound);
Decorate(ExpressionUnarySymbol ::= "!" ExpressionUnarySymbol) -> SemanticOperation
	:= (SemanticOperation[operator=NOT]
		Decorate(ExpressionUnarySymbol)
	);
Decorate(ExpressionUnarySymbol ::= "?" ExpressionUnarySymbol) -> SemanticOperation
	:= (SemanticOperation[operator=EMP]
		Decorate(ExpressionUnarySymbol)
	);
Decorate(ExpressionUnarySymbol ::= "+" ExpressionUnarySymbol) -> SemanticExpression
	:= Decorate(ExpressionUnarySymbol);
Decorate(ExpressionUnarySymbol ::= "-" ExpressionUnarySymbol) -> SemanticOperation
	:= (SemanticOperation[operator=NEG]
		Decorate(ExpressionUnarySymbol)
	);



Decorate(ExpressionClaim ::= ExpressionUnarySymbol) -> SemanticExpression
	:= Decorate(ExpressionUnarySymbol);
Decorate(ExpressionClaim ::= "<" Type ">" ExpressionClaim) -> SemanticClaim
	:= (SemanticClaim
		Decorate(Type)
		Decorate(ExpressionClaim)
	);



Decorate(ExpressionExponential ::= ExpressionClaim) -> SemanticExpression
	:= Decorate(ExpressionClaim);
Decorate(ExpressionExponential ::= ExpressionClaim "^" ExpressionExponential) -> SemanticOperation
	:= (SemanticOperation[operator=EXP]
		Decorate(ExpressionClaim)
		Decorate(ExpressionExponential)
	);



Decorate(ExpressionMultiplicative ::= ExpressionExponential) -> SemanticExpression
	:= Decorate(ExpressionExponential);
Decorate(ExpressionMultiplicative ::= ExpressionMultiplicative "*" ExpressionExponential) -> SemanticOperation
	:= (SemanticOperation[operator=MUL]
		Decorate(ExpressionMultiplicative)
		Decorate(ExpressionExponential)
	);
Decorate(ExpressionMultiplicative ::= ExpressionMultiplicative "/" ExpressionExponential) -> SemanticOperation
	:= (SemanticOperation[operator=DIV]
		Decorate(ExpressionMultiplicative)
		Decorate(ExpressionExponential)
	);



Decorate(ExpressionAdditive ::= ExpressionMultiplicative) -> SemanticExpression
	:= Decorate(ExpressionMultiplicative);
Decorate(ExpressionAdditive ::= ExpressionAdditive "+" ExpressionMultiplicative) -> SemanticOperation
	:= (SemanticOperation[operator=ADD]
		Decorate(ExpressionAdditive)
		Decorate(ExpressionMultiplicative)
	);
Decorate(ExpressionAdditive ::= ExpressionAdditive "-" ExpressionMultiplicative) -> SemanticOperation
	:= (SemanticOperation[operator=ADD]
		Decorate(ExpressionAdditive)
		(SemanticOperation[operator=NEG] Decorate(ExpressionMultiplicative))
	);



Decorate(ExpressionComparative ::= ExpressionAdditive) -> SemanticOperation
	:= Decorate(ExpressionAdditive);
Decorate(ExpressionComparative ::= ExpressionComparative "<" ExpressionAdditive) -> SemanticOperation
	:= (SemanticOperation[operator=LT]
		Decorate(ExpressionComparative)
		Decorate(ExpressionAdditive)
	);
Decorate(ExpressionComparative ::= ExpressionComparative ">" ExpressionAdditive) -> SemanticOperation
	:= (SemanticOperation[operator=GT]
		Decorate(ExpressionComparative)
		Decorate(ExpressionAdditive)
	);
Decorate(ExpressionComparative ::= ExpressionComparative "<=" ExpressionAdditive) -> SemanticOperation
	:= (SemanticOperation[operator=LE]
		Decorate(ExpressionComparative)
		Decorate(ExpressionAdditive)
	);
Decorate(ExpressionComparative ::= ExpressionComparative ">=" ExpressionAdditive) -> SemanticOperation
	:= (SemanticOperation[operator=GE]
		Decorate(ExpressionComparative)
		Decorate(ExpressionAdditive)
	);
Decorate(ExpressionComparative ::= ExpressionComparative "!<" ExpressionAdditive) -> SemanticOperation
	:= (SemanticOperation[operator=NOT]
		(SemanticOperation[operator=LT]
			Decorate(ExpressionComparative)
			Decorate(ExpressionAdditive)
		)
	);
Decorate(ExpressionComparative ::= ExpressionComparative "!>" ExpressionAdditive) -> SemanticOperation
	:= (SemanticOperation[operator=NOT]
		(SemanticOperation[operator=GT]
			Decorate(ExpressionComparative)
			Decorate(ExpressionAdditive)
		)
	);
Decorate(ExpressionComparative ::= ExpressionComparative "is" ExpressionAdditive) -> SemanticOperation
	:= (SemanticOperation[operator=IS]
		Decorate(ExpressionComparative)
		Decorate(ExpressionAdditive)
	);
Decorate(ExpressionComparative ::= ExpressionComparative "isnt" ExpressionAdditive) -> SemanticOperation
	:= (SemanticOperation[operator=NOT]
		(SemanticOperation[operator=IS]
			Decorate(ExpressionComparative)
			Decorate(ExpressionAdditive)
		)
	);



Decorate(ExpressionEquality ::= ExpressionComparative) -> SemanticOperation
	:= Decorate(ExpressionComparative);
Decorate(ExpressionEquality ::= ExpressionEquality "===" ExpressionComparative) -> SemanticOperation
	:= (SemanticOperation[operator=ID]
		Decorate(ExpressionEquality)
		Decorate(ExpressionComparative)
	);
Decorate(ExpressionEquality ::= ExpressionEquality "!==" ExpressionComparative) -> SemanticOperation
	:= (SemanticOperation[operator=NOT]
		(SemanticOperation[operator=ID]
			Decorate(ExpressionEquality)
			Decorate(ExpressionComparative)
		)
	);
Decorate(ExpressionEquality ::= ExpressionEquality "==" ExpressionComparative) -> SemanticOperation
	:= (SemanticOperation[operator=EQ]
		Decorate(ExpressionEquality)
		Decorate(ExpressionComparative)
	);
Decorate(ExpressionEquality ::= ExpressionEquality "!=" ExpressionComparative) -> SemanticOperation
	:= (SemanticOperation[operator=NOT]
		(SemanticOperation[operator=EQ]
			Decorate(ExpressionEquality)
			Decorate(ExpressionComparative)
		)
	);



Decorate(ExpressionConjunctive ::= ExpressionEquality) -> SemanticExpression
	:= Decorate(ExpressionEquality);
Decorate(ExpressionConjunctive ::= ExpressionConjunctive "&&" ExpressionEquality) -> SemanticOperation
	:= (SemanticOperation[operator=AND]
		Decorate(ExpressionConjunctive)
		Decorate(ExpressionEquality)
	);
Decorate(ExpressionConjunctive ::= ExpressionConjunctive "!&" ExpressionEquality) -> SemanticOperation
	:= (SemanticOperation[operator=NOT]
		(SemanticOperation[operator=AND]
			Decorate(ExpressionConjunctive)
			Decorate(ExpressionEquality)
		)
	);



Decorate(ExpressionDisjunctive ::= ExpressionConjunctive) -> SemanticOperation
	:= Decorate(ExpressionConjunctive);
Decorate(ExpressionDisjunctive ::= ExpressionDisjunctive "||" ExpressionConjunctive) -> SemanticOperation
	:= (SemanticOperation[operator=OR]
		Decorate(ExpressionDisjunctive)
		Decorate(ExpressionConjunctive)
	);
Decorate(ExpressionDisjunctive ::= ExpressionDisjunctive "!|" ExpressionConjunctive) -> SemanticOperation
	:= (SemanticOperation[operator=NOT]
		(SemanticOperation[operator=OR]
			Decorate(ExpressionDisjunctive)
			Decorate(ExpressionConjunctive)
		)
	);



Decorate(ExpressionConditional ::= "if" Expression__0 "then" Expression__1 "else" Expression__2)
	:= (SemanticOperation[operator=COND]
		Decorate(Expression__0)
		Decorate(Expression__1)
		Decorate(Expression__2)
	);



Decorate(Expression ::= ExpressionDisjunctive) -> SemanticExpression
	:= Decorate(ExpressionDisjunctive);
Decorate(Expression ::= ExpressionConditional) -> SemanticExpression
	:= Decorate(ExpressionConditional);



Decorate(StatementExpression ::= ";") -> SemanticStatementExpression
	:= (SemanticStatementExpression);
Decorate(StatementExpression ::= Expression ";") -> SemanticStatementExpression
	:= (SemanticStatementExpression Decorate(Expression));



Decorate(Statement ::= StatementExpression) -> SemanticStatementExpression
	:= Decorate(StatementExpression);
Decorate(Statement ::= Declaration) -> SemanticDeclaration
	:= Decorate(Declaration);



Decorate(Block ::= "{" Statement+ "}") -> SemanticBlock
	:= (SemanticBlock
		...ParseList(Statement, SemanticStatement)
	);



Decorate(DeclarationType ::= "type" IDENTIFIER "=" Type ";") -> SemanticDeclarationType
	:= (SemanticDeclarationType
		(SemanticTypeAlias[id=TokenWorth(IDENTIFIER)])
		Decorate(Type)
	);



Decorate(DeclarationVariable ::= "let" IDENTIFIER ":" Type "=" Expression ";") -> SemanticDeclarationVariable
	:= (SemanticDeclarationVariable[unfixed=false]
		(SemanticVariable[id=TokenWorth(IDENTIFIER)])
		Decorate(Type)
		Decorate(Expression)
	);
Decorate(DeclarationVariable ::= "let" "unfixed" IDENTIFIER ":" Type "=" Expression ";") -> SemanticDeclarationVariable
	:= (SemanticDeclarationVariable[unfixed=true]
		(SemanticVariable[id=TokenWorth(IDENTIFIER)])
		Decorate(Type)
		Decorate(Expression)
	);



Decorate(DeclarationClaim ::= "claim" Assignee ":" Type) -> SemanticDeclarationClaim
	:= (SemanticDeclarationClaim
		Decorate(Assignee)
		Decorate(Type)
	);



Decorate(DeclarationReassignment ::= "set" Assignee "=" Expression ";") -> SemanticDeclarationReassignment
	:= (SemanticDeclarationReassignment
		Decorate(Assignee)
		Decorate(Expression)
	);



Decorate(Declaration ::= DeclarationType) -> SemanticDeclarationType
	:= Decorate(DeclarationType);
Decorate(Declaration ::= DeclarationVariable) -> SemanticDeclarationVariable
	:= Decorate(DeclarationVariable);
Decorate(Declaration ::= DeclarationClaim) -> SemanticDeclarationClaim
	:= Decorate(DeclarationClaim);
Decorate(Declaration ::= DeclarationReassignment) -> SemanticDeclarationReassignment
	:= Decorate(StatementAssignment);



Decorate(Goal ::= #x02 #x03) -> SemanticGoal
	:= (SemanticGoal);
Decorate(Goal ::= #x02 Block #x03) -> SemanticGoal
	:= (SemanticGoal
		Decorate(Block)
	);



ParseList(ParseNode, ASTNode)(ParseNode__List ::= ParseNode) -> Sequence<ASTNode>
	:= [Decorate(ParseNode)];
ParseList(ParseNode, ASTNode)(ParseNode__List ::= ParseNode__List ","? ParseNode) -> Sequence<ASTNode>
	:= [
		...Decorate(ParseNode__List),
		Decorate(ParseNode),
	];



AccessKind(PropertyAccess ::= "."  (INTEGER | Word | "[" Expression "]")) -> NORMAL   := NORMAL;
AccessKind(PropertyAccess ::= "?." (INTEGER | Word | "[" Expression "]")) -> OPTIONAL := OPTIONAL;
AccessKind(PropertyAccess ::= "!." (INTEGER | Word | "[" Expression "]")) -> CLAIM    := CLAIM;
